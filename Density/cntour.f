      SUBROUTINE CNTOUR(A,MDIM,IMAX,JMAX,PZ,VALUE,I1,I2,I3,R11)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION  A(MDIM,*)
C      LOGICAL C(8000,2),B(40000)  ! Gerd
      LOGICAL C(18000,2),B(40000)
      COMMON/CNTR/R1,IM,JM
************************************************************************
*
* GENERAL CONTOUR-DRAWING ROUTINE.
*
*  ON INPUT:
*      A      = 2-D ARRAY OF POINTS DEFINING HEIGHT IN MAP-SPACE.
*      MDIM   = SIZE OF FIRST DIMENSION OF A.
*      IMAX   = LENGTH OF ONE SIDE OF ARRAY WITHIN A.
*      JMAX   = LENGTH OF THE OTHER SIDE OF THE ARRAY WITHIN A.
*      PZ     = VALUE OF Z DIRECTION IF 3-D PLOTS ARE BEING DRAWN.
*      VALUE  = VALUE OF CONTOUR.
*      I1,I2,I3 = 1,2,3 IF AN ORTHOSCOPIC PLOT IS WANTED, I.E. IF THE
*               VIEW IS ALONG Z, AND THE PLOT IS IN X AND Y, THIS IS THE
*               'NORMAL' PLOT. THIS OPTION HAS NEVER BEEN EXCERCISED
*               FULLY.
*  ON EXIT R11= LENGTH OF CONTOUR. THIS IS THE TOTAL LENGTH OF ALL LINES
*               MAKING UP THE CONTOUR, OPEN AND CLOSED.
************************************************************************
      IM = IMAX
      R1=0.D0
      JM=JMAX
C
C  SET UP STRING FOR LATER ANNOTATION OF CONTOURS.
C
      NUMBER = DABS(VALUE)
C
C  THE FOLLOWING CODE SUBTRACTS THE VALUE OF THE VALUE BEING SOUGHT
C  FROM ALL ELEMENTS OF A, SO THAT THE ZERO VALUE WILL BE SOUGHT
C  HEREAFTER.
C
      DO 10 I=1,IMAX
         DO 10 J=1,JMAX
   10 A(I,J)= A(I,J)-VALUE
      R11=R1
      JMAX1= JMAX-1
      IMAX1= IMAX-1
      DO 20 I=1,IMAX
         DO 20 J=1,JMAX1
            AA= A(I,J)
            AB= A(I,J+1)
C
C  IF A CONTOUR  PASSES CLOSE TO AN ELEMENT OF A, THE CORRESPONDING
C  ELEMENT OF LOGICAL ARRAY B IS SET.
C
            B(IMAX*(J-1)+I)=((AA.GE.0..AND.AB.LT.0.).OR.
     1                    (AA.LT.0..AND.AB.GE.0.))
   20 CONTINUE
      K=1
      DO 40 J=1,JMAX,JMAX1
         AB= A(1,J)
         DO 30 I=1,IMAX1
            AA= AB
            AB= A(I+1,J)
C
C  THE ARRAY C HAS THE SAME FUNCTION AS B, BUT ON THE BOUNDARY OF THE
C  AREA.
C
   30    C(I,K)=((AA.GE.0..AND.AB.LT.0.).OR.
     1                (AA.LT.0..AND.AB.GE.0.))
   40 K=2
C
C   DRAW UNCLOSED CONTOURS (IE STARTING ON A BOUNDARY).
C
      DO 50 I=1,IMAX1
         IF(C(I,1))CALL DRAW(I,1,1,A,B,C,PZ,I1,I2,I3,MDIM)
         IF(C(I,2))CALL DRAW(I+1,JMAX,3,A,B,C,PZ,I1,I2,I3,MDIM)
   50 CONTINUE
C
C  DRAW INTERIOR CONTOURS PASSING CLOSE TO BOUNDARY POINTS.
C
      DO 60 J=1,JMAX1
         IF(B((J-1)*IMAX+1)) CALL DRAW(1,J+1,4,A,B,C,PZ,I1,I2,I3,MDIM)
         IF(B(J*IMAX)) CALL DRAW(IMAX,J,2,A,B,C,PZ,I1,I2,I3,MDIM)
   60 CONTINUE
C
C  DRAW OTHER INTERIOR CONTOURS.
C
      DO 70 I=2,IMAX1
         DO 70 J=1,JMAX1
            IF(B((J-1)*IMAX+I)) CALL DRAW(I,J,2,A,B,C,PZ,I1,I2,I3,MDIM)
   70 CONTINUE
   80 CONTINUE
*
*  RESTORE "A" MATRIX TO IT'S ORIGINAL VALUE.
*
      DO 90 I=1,IMAX
         DO 90 J=1,JMAX
   90 A(I,J)= A(I,J)+VALUE
      RETURN
      END
